// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain
#include "NoiseUtils.hlsl"

// Create a RenderTexture with enableRandomWrite flag and set it
// with cs.SetTexture

// ColorInterval struct to store the color intervals
struct ColorInterval
{
    float start;
    float end;
    float4 colorStart;
    float4 colorEnd;
};

// Data that will be received from the C# script

// RWTexture2D<float4> is a type of texture that can be read and written to
RWTexture2D<float4> Result;
RWTexture2D<float4> HeightMap;

// RWStructuredBuffer<ColorInterval> is a type of array that can be read and written to
RWStructuredBuffer<ColorInterval> ColorIntervals;

// Simple variables that will be received from the C# script
uint width;
uint height;
float scale;
float offsetX;
float offsetY;
uint octaves;
float persistence;
float lacunarity;
uint numColors;
uint type;
float aspectRatio;

float GetNoise(float2 input)
{
    switch (type)
    {
        case 0:
            return Unity_GradientNoise_float(input);
        case 1:
            return abs(ClassicNoise(input) + 0.2);
        default:
            return Unity_GradientNoise_float(input);
    }
}


float FBM(float x, float y)
{
    float total = 0;
    float frequency = 1;
    float amplitude = 1;
    float maxValue = 0;

    for (uint i = 0; i < octaves; i++)
    {
        total += GetNoise(float2(x * frequency, y * frequency)) * amplitude;

        maxValue += amplitude;

        amplitude *= persistence;
        frequency *= lacunarity;
    }

    return total / maxValue;
}


float4 GetColorFromValue(float value)
{
    for (uint i = 0; i < numColors; i++)
    {
        if (value >= ColorIntervals[i].start && value <= ColorIntervals[i].end)
        {
            return lerp(ColorIntervals[i].colorStart, ColorIntervals[i].colorEnd, 
            (value - ColorIntervals[i].start) / (ColorIntervals[i].end - ColorIntervals[i].start));
        }
    }
    
    return float4(0, 0, 0, 1);
}

float4 GetHeight(float value)
{
	 if(value <= ColorIntervals[0].end)
        return float4(ColorIntervals[1].start,ColorIntervals[1].start,ColorIntervals[1].start,1);
    return float4(value,value,value,1);
}

// The main function that will be called by the C# script
[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= width || id.y >= height)
		return;
        
    float xCoord = ((float) id.x / width - 0.5) * aspectRatio * scale + offsetX;
    float yCoord = ((float) id.y / height - 0.5) * scale + offsetY;

    float sampled = FBM(xCoord, yCoord);
    float4 selectedColor = GetColorFromValue(sampled);
 
    Result[id.xy] = selectedColor;

    HeightMap[id.xy] = GetHeight(sampled);
}